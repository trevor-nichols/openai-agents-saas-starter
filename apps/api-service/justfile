set shell := ["bash", "-uc"]

env_runner := "cd ../../packages/starter_console && hatch run python -m starter_console.app --skip-env util run-with-env"
project_dir := justfile_directory()
repo_root := `cd ../.. && pwd`
env_compose := repo_root + "/.env.compose"
env_local := repo_root + "/apps/api-service/.env.local"
env_default := repo_root + "/apps/api-service/.env"

_check_env:
    @if [ ! -f "{{env_local}}" ] && [ ! -f "{{env_default}}" ]; then \
        echo "Error: No apps/api-service/.env.local or apps/api-service/.env file found. Create one before running tasks."; \
        exit 1; \
    fi

default:
    @just --list

bootstrap:
    @echo "Creating/refreshing the Hatch environment for api-service"
    cd {{project_dir}} && hatch env create

serve: _check_env
    @echo "Starting FastAPI via hatch run serve"
    {{env_runner}} {{env_compose}} {{env_local}} {{env_default}} -- bash -lc 'cd {{project_dir}} && hatch run serve'

migrate: _check_env
    @echo "Running Alembic migrations"
    {{env_runner}} {{env_compose}} {{env_local}} {{env_default}} -- bash -lc 'cd {{project_dir}} && python scripts/check_alembic_version.py && hatch run migrate'

migration-revision message: _check_env
    @echo "Creating Alembic revision: {{message}}"
    {{env_runner}} {{env_compose}} {{env_local}} {{env_default}} -- bash -lc 'cd {{project_dir}} && hatch run migration-revision "{{message}}"'

lint:
    cd {{project_dir}} && hatch run lint

typecheck:
    cd {{project_dir}} && hatch run typecheck

test:
    cd {{project_dir}} && hatch run test

test-unit:
    cd {{project_dir}} && hatch run test-unit

smoke-http:
	# Run HTTP smoke suite against a locally started api-service (sqlite + redis).
	# Respects SMOKE_* flags for optional surfaces; defaults keep AI/Billing off.
	bash scripts/smoke_http.sh

cleanup-run-events days="" batch="" sleep_ms="" dry_run="false": _check_env
    @echo "Cleaning up agent_run_events"
    {{env_runner}} {{env_compose}} {{env_local}} {{env_default}} -- bash -lc 'cd {{project_dir}} && hatch run python scripts/cleanup_run_events.py {{ if days != "" { "--days " + days } else { "" } }} {{ if batch != "" { "--batch " + batch } else { "" } }} {{ if sleep_ms != "" { "--sleep-ms " + sleep_ms } else { "" } }} {{ if dry_run == "true" { "--dry-run" } else { "" } }}'

cleanup-activity-events days="" batch="" sleep_ms="" dry_run="false": _check_env
    @echo "Cleaning up activity_events"
    {{env_runner}} {{env_compose}} {{env_local}} {{env_default}} -- bash -lc 'cd {{project_dir}} && hatch run python scripts/cleanup_activity_events.py {{ if days != "" { "--days " + days } else { "" } }} {{ if batch != "" { "--batch " + batch } else { "" } }} {{ if sleep_ms != "" { "--sleep-ms " + sleep_ms } else { "" } }} {{ if dry_run == "true" { "--dry-run" } else { "" } }}'
